// The Notices and Disclaimers for Ocean Worlds Autonomy Testbed for Exploration
// Research and Simulation can be found in README.md in the root directory of
// this repository.

// Excavation procedure based on JPL's Europa Lander reference mission.
// NOTE: this procedure is highly stubbed at the moment.

#include "plan-interface.h"
#include "europa_defs.h"

#define DIG_TRENCH_TIMEOUT 10

Real Lookup ExcavationTimeout;

LibraryAction Stub(In String desc);
LibraryAction Image(In String desc);

Excavation:
{
  Real Time;
  Boolean DeepEnough = false;

  // Excavation planning is not done within PLEXIL
  // so use of Checkpoints requires cooperation with interface
  // TODO: Verify arm position/safety
  Excavation1:
  {
    RepeatCondition !DeepEnough;
    InvariantCondition
      Time < (Excavation.EXECUTING.START + Lookup(ExcavationTimeout));

    Time = Lookup(time);

    DigAndFilm: Concurrence
    {
      Boolean DiggingSafe = true;

      DigTheTrench:
      {
        InvariantCondition DiggingSafe;
        EndCondition (Dig.command_handle == COMMAND_SUCCESS ||
                      Time >= (DigTheTrench.EXECUTING.START
                               + DIG_TRENCH_TIMEOUT));
        Dig: scoop_linear (BASE_FRAME, false, 1.5, 0.02, -0.155, 0.05, 0.1);
      }

      FilmDig:
      {
        Repeat DigTheTrench.state == EXECUTING;
        LibraryCall Image(desc="trench");
        Wait FILM_INTERVAL;
      }

      SenseDig:
      {
        Repeat DigTheTrench.state == EXECUTING;
        log_info ("Sensing dig...");
        DiggingSafe = true; // stub
        Wait SENSE_DIG_INTERVAL;
      }
    }

    RemoveTailings: LibraryCall Stub(desc="Removing tailings");
    ImageDig: LibraryCall Image(desc="dig");
    AssessDepth: DeepEnough = true; // stub
  }
}
