// The Notices and Disclaimers for Ocean Worlds Autonomy Testbed for Exploration
// Research and Simulation can be found in README.md in the root directory of
// this repository.

// Read and print telemetry values.

#include "common-interface.h"

TestTelemetry:
{
  Real FT[6]; // end effector force/torque
  Real ArmPose[7]; // Position (x,y,z) followed by orientation (x,y,z,w).

  log_info ("Starting telemetry tests...");

  // Testbed

  if (Lookup(UsingOceanWATERS)) log_info ("OceanWATERS is in use.");
  else log_info ("OceanWATERS is NOT in use.");
  endif;
  
  if (Lookup(UsingOWLAT)) log_info ("OWLAT is in use.");
  else log_info ("OWLAT is NOT in use.");
  endif;

  // Battery
  log_info ("Battery state of charge: ", Lookup(BatteryStateOfCharge));
  log_info ("Battery remaining useful life: ",
            Lookup(BatteryRemainingUsefulLife));
  log_info ("Battery temperature: ", Lookup(BatteryTemperature));

  // Pan/Tilt telemetry.

  // Note that the current version of OWLAT Sim has neither a pan/tilt
  // command nor pan/tilt telemetry.  Unless values are published to
  // /pan_tilt_position, these lookups will return the PLEXIL
  // adapter's default Lookup value, which is 0, when this plan is run
  // on OWLAT Sim.

  log_info ("Pan degees: ", Lookup(PanDegrees));
  log_info ("Pan radians: ", Lookup(PanRadians));
  log_info ("Tilt degees: ", Lookup(TiltDegrees));
  log_info ("Tilt radians: ", Lookup(TiltRadians));

  // Arm joint telemetry

  for (Integer j = 0; j < 7; j + 1) {
    log_info ("Position of arm joint ", j, " = ", Lookup(ArmJointPosition(j)));
    log_info ("Velocity of arm joint ", j, " = ", Lookup(ArmJointVelocity(j)));
    log_info ("Torque of arm joint ", j, " = ", Lookup(ArmJointTorque(j)));
    log_info ("Acceleration of arm joint ", j, " = ",
              Lookup(ArmJointAcceleration(j)));
  }

  FT = Lookup(ArmEndEffectorForceTorque);
  log_info ("Arm end effector force/torque: {");
  for (Integer i = 0; i < 6; i + 1) {
    log_info ("  ", FT[i]);
  }
  log_info ("}");

  ArmPose = Lookup(ArmPose);
  log_info ("Arm pose: {");
  for (Integer i = 0; i < 7; i + 1) {
    log_info ("  ", ArmPose[i]);
  }
  log_info ("}");

  log_info ("Telemetry tests finished.");
}
