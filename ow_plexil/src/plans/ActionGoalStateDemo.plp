// The Notices and Disclaimers for Ocean Worlds Autonomy Testbed for Exploration
// Research and Simulation can be found in README.md in the root directory of
// this repository.

// Unstow and stow the arm repeatedly. Pan the antenna in 15 degree
// increments when the 'Unstow' action goal status is 'ACTIVE' and in
// 15 degree decrements when the 'Stow' action goal status is
// 'ACTIVE'.  To terminate this plan, inject a power fault or just
// type Ctrl-C.

#include "plan-interface.h"

ActionGoalStateDemo:
{
  log_info ("Starting ActionGoalStateDemo plan...");
  LibraryCall Tilt (Degrees=0);

  Run: Concurrence
  {
    Real NewAngle = 0;
    ExitCondition Lookup(PowerFault);

    Stowing: UncheckedSequence
    {
      RepeatCondition true;
      StartCondition !Lookup(ArmFault);
      LibraryCall ArmUnstow();
      LibraryCall ArmStow();
    }
    Panning: UncheckedSequence
    {
      StartCondition !Lookup(AntennaPanFault);
      RepeatCondition true;

      if (Lookup(ActionGoalStatus("ArmUnstow")) == ACTION_ACTIVE) {
        NewAngle = (Lookup(PanDegrees) + 15) mod 360;
      }
      else if (Lookup(ActionGoalStatus("ArmStow")) == ACTION_ACTIVE) {
        NewAngle = (Lookup(PanDegrees) - 15) mod 360;
      }
      endif;

      if (NewAngle > 180) {
        NewAngle = NewAngle - 360;
      }
      endif;

      LibraryCall Pan (Degrees=NewAngle);
    }
  }
  log_info ("ActionGoalStateDemo plan finished.");
}
